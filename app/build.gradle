apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.pandulapeter.campfire"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 21
        versionName "1.0.1"
        vectorDrawables.useSupportLibrary = true
    }
    buildFeatures.dataBinding = true
    signingConfigs {
        def keystoreProperties = new Properties()
        internal {
            keystoreProperties.load(new FileInputStream(rootProject.file("app/internal.keystore.properties")))
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        release {
            keystoreProperties.load(new FileInputStream(rootProject.file("app/internal.keystore.properties")))
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            debuggable true
            shrinkResources false
            minifyEnabled false
            versionNameSuffix "-debug"
        }
        internal {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.internal
            versionNameSuffix "-internal"
        }
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude "META-INF/main.kotlin_module"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}
androidExtensions {
    experimental = true
}
dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"

    // Service locator
    implementation "org.koin:koin-androidx-viewmodel:2.1.6"

    // Networking
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation("com.squareup.retrofit2:retrofit:$retrofitVersion") { exclude module: "okhttp" }
    def okHttpVersion = "4.8.1"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"

    // Persistence
    def roomVersion = "2.3.0-alpha02"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Image loading
    def glideVersion = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // Process handling
    implementation "com.jakewharton:process-phoenix:2.0.0"

    // Analytics
    implementation "com.google.firebase:firebase-core:17.5.1"

    // Deep linking
    implementation "com.google.firebase:firebase-dynamic-links:19.1.1"

    // Crash reporting
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.9"

    // UI components
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.3.0-alpha03"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.2"
    implementation "com.simplecityapps:recyclerview-fastscroll:2.0.0"

    // In-app purchases
    implementation "com.android.billingclient:billing:1.1"

    // Debug menu
    def beagleVersion = "2.1.2"
    def beagleUi = "bottom-sheet"
    debugImplementation "com.github.pandulapeter.beagle:ui-$beagleUi:$beagleVersion"
    debugImplementation "com.github.pandulapeter.beagle:log-okhttp:$beagleVersion"
    internalImplementation "com.github.pandulapeter.beagle:ui-$beagleUi:$beagleVersion"
    internalImplementation "com.github.pandulapeter.beagle:log-okhttp:$beagleVersion"
    releaseImplementation "com.github.pandulapeter.beagle:noop:$beagleVersion"
    releaseImplementation "com.github.pandulapeter.beagle:log-okhttp-noop:$beagleVersion"
}
apply plugin: "com.google.gms.google-services"