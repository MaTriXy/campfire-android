apply plugin: "com.android.application"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.pandulapeter.campfire"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 19
        versionName "0.9.3"
        vectorDrawables.useSupportLibrary = true
    }
    dataBinding {
        enabled = true
    }
    signingConfigs {
        def keystoreProperties = new Properties()
        internal {
            keystoreProperties.load(new FileInputStream(rootProject.file("app/internal.keystore.properties")))
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        release {
            keystoreProperties.load(new FileInputStream(rootProject.file("app/release.keystore.properties")))
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            debuggable true
            shrinkResources false
            minifyEnabled false
            versionNameSuffix "-debug"
        }
        internal {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.internal
            versionNameSuffix "-internal"
        }
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude "META-INF/main.kotlin_module"
    }
}
repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}
androidExtensions {
    experimental = true
}
dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"

    // Service locator
    implementation "org.koin:koin-androidx-viewmodel:1.0.2"

    // Networking
    def retrofitVersion = "2.3.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Persistence
    def roomVersion = "2.1.0-alpha04"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Image loading
    def glideVersion = "4.9.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // Process handling
    implementation "com.jakewharton:process-phoenix:2.0.0"

    // Analytics
    implementation "com.google.firebase:firebase-core:16.0.7"

    // Deep linking
    implementation "com.google.firebase:firebase-dynamic-links:16.1.7"

    // Crash reporting
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.9"

    // UI components
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.1.0-alpha04"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    implementation "com.simplecityapps:recyclerview-fastscroll:1.0.20"

    // In-app purchases
    implementation "com.android.billingclient:billing:1.1"
}
apply plugin: "com.google.gms.google-services"